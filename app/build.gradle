apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

File albumMapJSONfile = new File("album_map.json")
String albumMapJSONcleanText = albumMapJSONfile.text
        .replaceAll("\\s", "")              // eliminate whitespace
        .replaceAll("\"", "\\\\\"")         // backslash-escape double-quotes
String albumMapJSONcleanString = "\"" + albumMapJSONcleanText + "\""

android {
    signingConfigs {
        cuSigningConfig {
            keyAlias keystoreProperties['keyAlias']
            storeFile file(keystoreProperties['storeFile'])
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 26
    defaultConfig {
        applicationId "org.ursichfamily.apps.cuchristmas2017"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 4
        versionName '2.1.0'
    }
    buildTypes {
        release {
            buildConfigField("String", "albumMapJSON", albumMapJSONcleanString)
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.cuSigningConfig
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.google.android.gms:play-services-auth:11.8.0'

    // From https://developers.google.com/android/guides/http-auth ...
    // Google Api Client library and Android extension
    implementation 'com.google.api-client:google-api-client:1.22.0'
    implementation 'com.google.api-client:google-api-client-android:1.22.0'
}

apply plugin: 'com.google.gms.google-services'